#!/usr/bin/env perl6
# Copyright 2016 cygx <cygx@cpan.org>
# Distributed under the Boost Software License, Version 1.0

use v6;
use nqp;

nqp::loadbytecode('MoarAS.moarvm');
my \as = nqp::gethllsym('nqp', 'MoarAS::Compiler');

sub dest($_) {
    .subst(/'.asm'?$/, '.moarvm');
}

proto MAIN(|c) is export {
    CATCH {
        note "$_\n" ~ .backtrace.grep(none *.is-hidden, *.is-setting)[^2].join;
        exit 1;
    }
    {*}
}

multi MAIN(Str $src?, Bool :parse(:$p)!) {
    $src ?? as.parse_file($src)
         !! as.parse_fh(nqp::getstdin());
}

multi MAIN(Str $src?, Bool :dump(:$d)!) {
    put ($src ?? as.parse_file($src)
              !! as.parse_fh(nqp::getstdin())).dump;
}

multi MAIN(Str $src!, Bool :compile(:$c)!) {
    as.compile_file($src, dest($src))
}

multi MAIN(Str $src?, Str :compile-to($dest)!) {
    $src ?? as.compile_file($src, $dest)
         !! as.compile_fh(nqp::getstdin(), $dest);
}

multi MAIN(Str $src?, Bool :run(:$r)!) {
    $src ?? as.eval_file($src)
         !! as.eval_fh(nqp::getstdin());
}
